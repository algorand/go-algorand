// Allows some account to execute periodic withdrawal of funds.
// This is a contract account.
//
// This allows TMPL_RCV to withdraw TMPL_AMT every
// TMPL_PERIOD rounds for TMPL_DUR after every multiple
// of TMPL_PERIOD.
//
// After TMPL_TIMEOUT, all remaining funds in the escrow
// are available to TMPL_RCV.
//
// Parameters:
//  - TMPL_RCV: address which is authorized to make withdrawals
//  - TMPL_PERIOD: the time between a pair of withdrawal periods
//  - TMPL_DUR: the duration of a withdrawal period
//  - TMPL_AMT: the maximum number of funds allowed for a single withdrawal
//  - TMPL_LEASE: string to use for the transaction lease
//  - TMPL_TIMEOUT: the round at which the account expires
//  - TMPL_FEE: maximum fee used by the withdrawal transaction
txn TypeEnum
int 1
==
txn Fee
int TMPL_FEE
<=
&&
txn FirstValid
int TMPL_PERIOD
%
int 0
==
&&
txn LastValid
int TMPL_DUR
txn FirstValid
+
==
&&
txn Lease
byte base64 TMPL_LEASE
==
&& // is Payment and ok Fee and on period and correct duration and good lease
txn CloseRemainderTo
global ZeroAddress
==
txn Receiver
addr TMPL_RCV
==
&&
txn Amount
int TMPL_AMT
==
&& // no close and good Receiver and good amount
txn CloseRemainderTo
addr TMPL_RCV
==
txn Receiver
global ZeroAddress
==
&&
txn FirstValid
int TMPL_TIMEOUT
>
&&
txn Amount
int 0
==
&& // good close to and after timeout and 0 Amount
|| // normal payment or close
&& // (normal payment or close) and preamble checks
